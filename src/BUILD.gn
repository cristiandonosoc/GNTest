# Copyright 2018, Cristi√°n Donoso.
# This code has a BSD license. See LICENSE.

declare_args() {
  sdl_include_path = getenv("SDL2_SDK") + "/include"
  sdl_lib_path = getenv("SDL2_SDK") + "/lib"
  vulkan_include_path = getenv("VULKAN_SDK") + "/include"
  vulkan_lib_path = getenv("VULKAN_SDK") + "/lib"
}

config("sdl2_config") {
  if (target_os == "win" && compiler == "msvc") {
    include_dirs = [ "../includelnk" ]
    libs = [ "../liblnk/SDL2/SDL2.lib" ]
    ldflags = [ "/SUBSYSTEM:CONSOLE" ]
  } else if (target_os == "mac" && compiler == "clang") {
    # NOTE: SDL2 should be installed as a framework in /Library/Frameworks
    ldflags = [
      "-framework", "SDL2",
    ]
  } else {
    print("TARGET OS:", target_os, "COMPILER:", compiler)
    assert(false, "Unknown OS/Compiler combo")
  }
}

config("vulkan_config") {
  if (target_os == "win" && compiler == "msvc") {
    include_dirs = [ "../includelnk" ]
    libs  = [ "../liblnk/vulkan/vulkan-1.lib" ]
  } else if (target_os == "mac" && compiler == "clang") {
    # IMPORTANT: vulkan for mac requires certain env variables to be set in
    #            order to work correctly. Eg:
    #
    # export VULKAN_SDK="/Users/donosoc/SDKs/vulkansdk-macos-1.1.82.0/macOS"

    # Installable Client Driver interfaces:
    # The ICD are the drivers available to expose the physical devices.
    # This env variable allows the loader to find the ICDs molten provides.
    #   export VK_ICD_FILENAMES="$VULKAN_SDK/etc/vulkan/icd.d/MoltenVK_icd.json"

    # Point the loader to the manifest where the layers are specified.
    # export VK_LAYER_PATH = "$VULKAN_SDK/etc/vulkan/explicit_layers.d"
    print("${vulkan_include_path}")
    include_dirs = [
      "$vulkan_include_path",
    ]
    libs = [
      "${vulkan_lib_path}/libvulkan.dylib",
    ]
  } else {
    print("TARGET OS:", target_os, "COMPILER:", compiler)
    assert(false, "Unknown OS/Compiler combo")
  }
}

source_set("vulkan_lib") {
  sources = [
    "vulkan_context.cc",
    "vulkan_context.h",
    "vulkan_utils.cc",
    "vulkan_utils.h",
  ]
  configs += [
    ":vulkan_config",
  ]
}

executable("vulkan_test") {
  sources = [ "vulkan_test.cc" ]
  configs += [
    ":vulkan_config",
    ":sdl2_config"
  ]
  deps = [
    ":vulkan_lib",
    "//src/utils:utils",
  ]
}

executable("warhol") {
  sources = [
    "main.cc"
  ]
  configs += [
    ":sdl2_config",
  ]
}
