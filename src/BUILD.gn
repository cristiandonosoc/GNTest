# Copyright 2018, Cristi√°n Donoso.
# This code has a BSD license. See LICENSE.

declare_args() {
  sdl2_include_path = getenv("SDL2_SDK") + "/include"
  sdl2_lib_path = getenv("SDL2_SDK") + "/lib"
  vulkan_include_path = getenv("VULKAN_SDK") + "/include"
  vulkan_lib_path = getenv("VULKAN_SDK") + "/lib"
}

config("sdl2_config") {
  handled = false
  if (target_os == "win" && compiler == "msvc") {
    include_dirs = [ "../includelnk" ]
    libs = [ "../liblnk/SDL2/SDL2.lib" ]
    ldflags = [ "/SUBSYSTEM:CONSOLE" ]
    handled = true
  } else if (target_os == "mac" && compiler == "clang") {
    # NOTE: SDL2 should be installed as a framework in /Library/Frameworks
    ldflags = [ "-framework", "SDL2" ]
    handled = true
  } else if (target_os == "linux") {
    if (compiler == "clang") {
      include_dirs = [ "$sdl2_include_path" ]
      ldflags = [ "-lSDL2" ]
      handled = true
    }
  }

  if (!handled) {
    assert(false, "SDL2: Unhandled OS/Compiler combo: $target_os/$compiler")
  }
}

config("vulkan_config") {
  handled = false
  if (target_os == "win" && compiler == "msvc") {
    include_dirs = [ "../includelnk" ]
    libs  = [ "../liblnk/vulkan/vulkan-1.lib" ]
    handled = true
  } else if (target_os == "mac" && compiler == "clang") {
    # IMPORTANT: vulkan for mac requires certain env variables to be set in
    #            order to work correctly. Eg:
    #
    # export VULKAN_SDK="/Users/donosoc/SDKs/vulkansdk-macos-1.1.82.0/macOS"

    # Installable Client Driver interfaces:
    # The ICD are the drivers available to expose the physical devices.
    # This env variable allows the loader to find the ICDs molten provides.
    #   export VK_ICD_FILENAMES="$VULKAN_SDK/etc/vulkan/icd.d/MoltenVK_icd.json"

    # Point the loader to the manifest where the layers are specified.
    # export VK_LAYER_PATH = "$VULKAN_SDK/etc/vulkan/explicit_layers.d"
    include_dirs = [ "${$vulkan_include_path}" ]
    libs = [ "${vulkan_lib_path}/libvulkan.dylib" ]
    handled = true
  } else if (target_os == "linux") {
    if (compiler == "clang") {
      include_dirs = [ "${vulkan_include_path}" ]
      libs = [ "-lvulkan" ]
      ldflags = [ "-L${vulkan_lib_path}" ]
      handled = true
    }
  }

  if (!handled) {
    assert(false, "Vulkan: Unhandled OS/Compiler combo: $target_os/$compiler")
  }
}

source_set("vulkan_lib") {
  sources = [
    "vulkan_context.cc",
    "vulkan_context.h",
    "vulkan_utils.cc",
    "vulkan_utils.h",
  ]
  configs += [
    ":sdl2_config",
    ":vulkan_config",
  ]
}

# Executables -------------------------------------------------------------------

executable("warhol") {
  sources = [
    "main.cc"
  ]
  deps = [
    ":vulkan_lib",
    "//src/utils",
  ]
  configs += [
    ":sdl2_config",
    ":vulkan_config",
  ]
}

executable("sdl2_test") {
  sources = [
    "sdl2_test.cc",
  ]
  configs += [
    ":sdl2_config",
  ]
}
