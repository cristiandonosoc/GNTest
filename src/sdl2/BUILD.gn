# Copyright 2018, Cristi√°n Donoso.
# This code has a BSD license. See LICENSE.

declare_args() {
  sdl2_include_path = getenv("SDL2_SDK")
  sdl2_lib_path = getenv("SDL2_SDK")
}

config("sdl2_config") {
  handled = false
  include_dirs = [ "$sdl2_include_path" ]
  if (target_os == "win" && compiler == "msvc") {
    libs = [ "${sdl2_lib_path}/SDL2/SDL2.lib" ]
    handled = true
  } else if (target_os == "mac" && compiler == "clang") {
    # NOTE: In OSX, the headers nornally are in:
    # /Library/Frameworks/SDL2.framework/Headers/
    # TODO(Cristian): Should this be enforced always here?
    # NOTE: SDL2 should be installed as a framework in /Library/Frameworks
    ldflags = [ "-framework", "SDL2", "-F/Library/Frameworks", ]
    handled = true
  } else if (target_os == "linux") {
    if (compiler == "clang") {
      libs = [ "-lSDL2" ]
      ldflags = [ "-L${sdl2_lib_path}" ]
      handled = true
    }
  }

  if (!handled) {
    assert(false, "SDL2: Unhandled OS/Compiler combo: $target_os/$compiler")
  }
}

source_set("sdl2") {
  sources = [
    "def.h",  # This is the header to import generally.
    "input.cc",
    "input.h",
    "sdl_context.cc",
    "sdl_context.h",
    # "utils.cc",
    # "utils.h",
  ]

  public_configs = [
    ":sdl2_config",
  ]

  deps = [
    "//src/input",
    "//src/graphics/GL",
    "//src/utils",
  ]
}

executable("sdl2_test") {
  sources = [
    "sdl2_test.cc",
  ]
  configs += [
    ":sdl2_config",
  ]
}
