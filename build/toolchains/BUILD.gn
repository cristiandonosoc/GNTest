# msvc
# ------------------------------------------------------------------------------

toolchain("msvc") {
  tool("cxx") {
    depsformat = "msvc"
    pdbname = "{{output}}_cc.pdb"
    command = "cl.exe /nologo /showIncludes /FC /FS /Fd$pdbname {{include_dirs}} {{cflags}} {{cflags_cc}} /c {{source}} /Fo{{output}}"
    outputs = [
      "{{source_out_dir}}/{{source_name_part}}.o" ,
    ]
    description = "CXX {{output}}"
  }

  tool("link") {
    command = "link.exe /nologo {{ldflags}} /OUT:{{output}} /PDB:{{output}}.pdb {{inputs}} {{solibs}} {{libs}}"
    outputs = [
      "{{root_out_dir}}/{{target_output_name}}.exe"
    ]
    description = "LINK {{output}}"
  }


  stamp_path = rebase_path("//build/toolchains/msvc_stamp.py", root_build_dir)

  tool("stamp") {
    command = "$python_path $stamp_path {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "copy {{source}} {{output}}"
    description = "COPY {{source}} -> {{output}}"
  }
}

# clang
# ------------------------------------------------------------------------------

toolchain("clang") {
  tool("cxx") {
    depfile = "{{output}}.d"
    depsformat = "gcc"
    command = "clang++ -MMD -MF $depfile {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    outputs = [
      "{{source_out_dir}}/{{source_name_part}}.o" ,
    ]
    # description = "CXX {{output}}"
  }

  tool("link") {
    command = "clang++ {{inputs}} {{solibs}} {{ldflags}} {{libs}} -o {{output}}"
    outputs = [
      "{{root_out_dir}}/{{target_output_name}}"
    ]
    # description = "LINK {{output}}"
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp {{source}} {{output}}"
    description = "COPY {{source}} -> {{output}}"
  }
}
