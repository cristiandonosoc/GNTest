
# Args
# ------------------------------------------------------------------------------

declare_args() {
  is_debug = true
}

# OS/Compiler Targets
# ------------------------------------------------------------------------------

# If we don't explicitly define a target, we're compiling for the host.
if (target_os == "") {
  target_os = host_os
}

# Host to compiler mapping
compiler = ""
if (target_os == "win") {
  compiler = "msvc"
  set_default_toolchain("//build/toolchains:msvc")
}
# TODO: Linux
if (target_os == "mac") {
  compiler = "clang"
  set_default_toolchain("//build/toolchains:clang")
}

assert(compiler != "", "Unsupported OS")

# Configs
# ------------------------------------------------------------------------------

# Default configs for every build (cased by OS target)
_configs = [
  "//:default_include_dirs",
  "//build/config/compiler:compiler",
  "//build/config/compiler:default_warnings",
]

if (is_debug) {
  _configs += [
    "//build/config/compiler:debug",
  ]
} else {
  _configs += [
    "//build/config/compiler:release",
  ]
}

set_defaults("executable") {
  configs = _configs
}

set_defaults("source_set") {
  configs = _configs
}
