
# Args -------------------------------------------------------------------------

declare_args() {
  is_debug = true
}

# OS Targets -------------------------------------------------------------------

if (target_os == "") {
  target_os = host_os
}

if (target_os == "win") {
  is_windows = true
}

assert(is_windows, "Only windows supported for now.")

# Configs ----------------------------------------------------------------------

# Default configs for every build (cased by OS target)
_configs = [
  "//build/config/compiler:default_include_dirs",
  "//build/config/compiler:default_warnings",
  "//build/config/compiler:compiler",
]

if (is_debug) {
  _configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
    "//build/config/compiler:symbols",
  ]
} else {
  _configs += [
    "//build/config:release",
    "//build/config/compiler:optimize",
  ]
}

set_defaults("executable") {
  configs = _configs
}

set_defaults("source_set") {
  configs = _configs
}

set_default_toolchain("//build/toolchain/msvc/:msvc")
