# Copyright 2019, Cristi√°n Donoso.
# This code has a BSD license. See LICENSE.

declare_args() {
  sdl2_include_path = getenv("SDL2_SDK")
  sdl2_lib_path = getenv("SDL2_SDK")
}

config("sdl2_config") {
  handled = false
  if ("$sdl2_include_path" != "") {
    include_dirs = [ "$sdl2_include_path" ]
  }
  if (target_os == "win" && compiler == "msvc") {
    if ("$sdl2_lib_path" != "") {
      libs = [ "${sdl2_lib_path}/SDL2/SDL2.lib" ]
    }
    handled = true
  } else if (target_os == "mac" && compiler == "clang") {
    # NOTE: In OSX, the headers nornally are in:
    # /Library/Frameworks/SDL2.framework/Headers/
    # TODO(Cristian): Should this be enforced always here?
    # NOTE: SDL2 should be installed as a framework in /Library/Frameworks
    ldflags = [ "-framework", "SDL2", "-F/Library/Frameworks", ]
    handled = true
  } else if (target_os == "linux") {
    if (compiler == "clang") {
      libs = [ "-lSDL2" ]
      if ("$sdl2_lib_path" != "") {
        ldflags = [ "-L${sdl2_lib_path}" ]
      }
      handled = true
    }
  }

  if (!handled) {
    assert(false, "SDL2: Unhandled OS/Compiler combo: $target_os/$compiler")
  }
}

source_set("sdl_vulkan") {
  sources = [
    "sdl_vulkan_window_manager.cc",
    "sdl_vulkan_window_manager.h",
  ]

  public_configs = [
    ":sdl2_config",
  ]

  deps = [
    "//warhol/input",
    "//warhol/utils",
  ]
}
